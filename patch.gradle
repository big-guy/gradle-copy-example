
repositories {
   mavenCentral()
}

configurations {
   patch
   patchDiff
   patchDiffExtract
}

ext.baseTargetDir = "$buildDir/baseTarget"
ext.patchBaseVersion = {
   if (project.hasProperty('baseRef')) {
      "git describe ${project.properties.baseRef}".execute().text.trim() 
   } else {
     project.properties['baseVersion'] ?: '1.0.0'
   }
}
ext.patchFileName = file("$buildDir/distributions/${project.name}-${project.patchBaseVersion()}_to_${project.version}-patch.zip")

dependencies {
   patch ":${project.name}:${patchBaseVersion()}@zip"

   patchDiffExtract 'com.alexkasko.delta:delta-diff:1.1'

   // fatjar can be built from fork available: https://github.com/big-guy/delta-updater
   patchDiff files("${rootDir}/../delta-updater/build/libs/delta-updater.jar")
}

task extractPatch(type: Copy, dependsOn: configurations.patch ) {
   into baseTargetDir
   from {
      configurations.patch.collect { zipTree(it) }
   }
}

task assemblePatchExtract(type: JavaExec, dependsOn: [ 'copyConfiguration', 'extractPatch' ]) {
   inputs.files targetDir, baseTargetDir
   outputs.files patchFileName 

   classpath configurations.patchDiffExtract
   main 'com.alexkasko.delta.DiffLauncher'

   args baseTargetDir, targetDir, "-o", patchFileName 
} 

task assemblePatch(type: JavaExec, dependsOn: [ 'copyConfiguration' ]) {
   inputs.files targetDir, baseTargetDir
   outputs.files patchFileName 

   classpath configurations.patchDiff
   main 'com.alexkasko.delta.DiffLauncher'

   doFirst() {
      args "zip://" + configurations.patch.singleFile, targetDir, "-o", patchFileName 
   }
} 
